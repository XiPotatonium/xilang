File = { SOI ~ (Modules | ExternMod | UseDeclarations)* ~ Class* ~ EOI }

Modules = { "mod" ~ Id ~ Semi }
ExternMod = {"extern" ~ "mod" ~ Id ~ Semi}
UseDeclarations = { "use" ~ PathExpr ~ ("as" ~ Id)? ~ Semi }

AttributeLst = { "#" ~ "[" ~ Attribute ~ ("," ~ Attribute)* ~ "]" }
Attribute = { Id ~ ("(" ~ (LiteralExpr ~ ",")* ~ LiteralExpr? ~ ")")? }

Class = { 
    AttributeLst* ~ (KwClass | KwStruct | KwInterface) ~ Id ~ GenericParamsDecl? ~ ExtendsOrImpls? ~
    "{" ~ (AttributeLst* ~ (CCtor | Ctor | Method | StaticField | NonStaticField))* ~ "}" 
}

ExtendsOrImpls = { ":" ~ PathExpr ~ ("," ~ PathExpr)* }
GenericParamsDecl = { "<" ~ GenericParamDecl ~ ("," ~ GenericParamDecl)* ~ ">" }
GenericParamDecl = { Id ~ (":" ~ PathExpr ~ ("+" ~ PathExpr)*)? }
IdWithGenericParams = { Id ~ ("<" ~ Type ~ ("," ~ Type)* ~ ">")? }

StaticField = { "static" ~ Id ~ ":" ~ Type ~ Semi }
NonStaticField = { "let" ~ Id ~ ":" ~ Type ~ Semi }

CCtor = { "static" ~ BlockExpr }
Ctor = { "Self" ~ GenericParamsDecl? ~ Params ~ (":" ~ KwBase ~ Args )? ~ BlockExpr }
Method = { (KwOverride | KwVirtual)* ~ "fn" ~ Id ~ GenericParamsDecl? ~  Params ~ ("->" ~ Type)? ~ (BlockExpr | Semi) }
Params = { "(" ~ ((KwLSelf | Id ~ ":" ~ Type) ~ ("," ~ Id ~ ":" ~ Type)* ~ ","?)? ~ ")" }

Pattern = { Id | TuplePattern }
TuplePattern = { "(" ~ (Pattern ~ ",")* ~ Pattern? ~ ")" }

Type = { ArrType | NonArrType }
BasicType = _{ KwBool | KwChar | KwI32 | KwF64 | KwString }
TupleType = { "(" ~ (Type ~ ",")* ~ Type? ~ ")" }
NonArrType = _{
    BasicType
    | KwUSelf
    | PathExpr
    | TupleType
}
ArrType = { NonArrType ~ "[" ~ "]" }

Stmt = { LetStmt | ExprWithoutBlock ~ Semi | ExprWithBlock ~ Semi? }
LetStmt = { "let" ~ Pattern ~ (":" ~ Type)? ~ (Eq ~ Expr)? ~ Semi }
Expr = _{ ExprWithBlock | ExprWithoutBlock }
ExprWithBlock = _{ BlockExpr | LoopExpr | IfExpr }
ExprWithoutBlock = _{
    ContinueExpr
    | BreakExpr
    | ReturnExpr
    | AssignExpr
    | OpExpr
}

LoopExpr = { "loop" ~ BlockExpr }
IfExpr = { "if" ~ Expr ~ BlockExpr ~ ("else" ~ (BlockExpr | IfExpr))? }
BlockExpr = { "{" ~ Stmt* ~ ExprWithoutBlock? ~ "}" }

ContinueExpr = { "continue" }
BreakExpr = { "break" ~ Expr? }
ReturnExpr = { "return" ~ Expr? }

GroupedExpr = { "(" ~ Expr ~ ")" }
// Actually Type here will not include PathExpr. Maybe only BasicType but I'm not sure for now
PrimaryExpr = { LiteralExpr | KwLSelf | GroupedExpr | ExprWithBlock | IdWithGenericParams | Type }
Args = { "(" ~ (Expr ~ ",")* ~ Expr? ~ ")" }
ObjAccessExpr = { "." ~ IdWithGenericParams }
PathAccessExpr = { "::" ~ IdWithGenericParams }
ArrAccessExpr = { "[" ~ Expr ~ "]" }
CallExpr = { PrimaryExpr ~ (Args | ObjAccessExpr | PathAccessExpr | ArrAccessExpr)* }
NewExpr = { "new" ~ Type ~ (Args | ArrAccessExpr) | CallExpr }
UnaryExpr = { (Not | Plus | Minus)* ~ NewExpr }
CastExpr = { UnaryExpr ~ ("as" ~ Type)* }
MulExpr = { CastExpr ~ ((Star | Slash | Percent) ~ CastExpr)* }
AddExpr = { MulExpr ~ ((Plus | Minus) ~ MulExpr)* }
CompExpr = { AddExpr ~ ((Le | Lt | Ge | Gt) ~ AddExpr)* }
EqExpr = { CompExpr ~ ((EqEq | Ne) ~ CompExpr)* }
LogAndExpr = { EqExpr ~ ("&&" ~ EqExpr)* }
LogOrExpr = { LogAndExpr ~ ("||" ~ LogAndExpr)* }
OpExpr = { LogOrExpr }
AssignExpr = { LogOrExpr ~ Eq ~ LogOrExpr }

LiteralExpr = {
    KwTrue
    | KwFalse
    | KwNull
    | EmptyLiteral
    | IntLiteral
    | FloatLiteral
    | StringLiteral
    | CharLiteral
}

EmptyLiteral = { "(" ~ ")" }
IntLiteral = { ASCII_DIGIT+ }
FloatLiteral = @{ ASCII_DIGIT* ~ "." ~ ASCII_DIGIT+ }
StringLiteral = @{ "\"" ~ ("\\" ~ ANY | !"\"" ~ ANY)* ~ "\"" }
CharLiteral = @{ "'" ~ ("\\" ~ ANY | !"'" ~ ANY)* ~ "'" }

PathExpr = { (IdWithGenericParams | KwCrate | KwSuper) ~ ("::" ~ (IdWithGenericParams | KwSuper))* }
Id = @{ 
    KeyWord ~ (ASCII_ALPHANUMERIC | "_")+ 
    | !KeyWord ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* 
}

KwClass = { "class" }
KwStruct = { "struct" }
KwInterface = { "interface" }
KwBase = { "base" }
KwLSelf = { "self" }
KwUSelf = { "Self" }
KwVirtual = { "virtual" }
KwOverride = { "override" }
KwPub = { "pub" }
KwPriv = { "priv" }
KwSuper = { "super" }
KwCrate = { "crate" }

KwBool = { "bool" }
KwChar = { "char" }
KwU8 = { "u8" }
KwI8 = { "i8" }
KwU16 = { "u16" }
KwI16 = { "i16" }
KwU32 = { "u32" }
KwI32 = { "i32" }
KwU64 = { "u64" }
KwI64 = { "i64" }
KwUSize = { "usize" }
KwISize = { "isize" }
KwF32 = { "f32" }
KwF64 = { "f64" }
KwTrue = { "true" }
KwFalse = { "false" }
KwNull = { "null" }
KwString = { "string" }

KeyWord = @{ 
    KwClass | KwStruct | KwInterface | "enum" | "union" | "fn" | "let" | "where" | "new"
    | "static" | "const" | "mut" | KwPub | KwPriv | KwVirtual | KwOverride
    | KwLSelf | KwUSelf | KwBase | KwSuper | KwCrate | "extern" | "mod" | "use"
    | "as" | "break" | "continue" | "else" | "for" | "if" | "in" | "loop" | "match" | "return" | "while"
    | KwBool | KwChar | KwU8 | KwI8 | KwU16 | KwI16 | KwU32 | KwI32 | KwU64 | KwI64 | KwUSize | KwISize | KwF32 | KwF64
    | KwFalse | KwTrue | KwNull | KwString
    | "yield" | "typeof"
}

Eq = { "=" }
EqEq = { "==" }
Ne = { "!=" }
Ge = { ">=" }
Gt = { ">" }
Le = { "<=" }
Lt = { "<" }
Plus = { "+" }
Minus = { "-" }
Star = { "*" }
Slash = { "/" }
Percent = { "%" }
Not = { "!" }
Semi = { ";" }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" | "//" ~ (!NEWLINE ~ ANY)* }

